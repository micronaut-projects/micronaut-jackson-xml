plugins {
    id 'java-library'
    id 'org.graalvm.buildtools.native'
    id "io.micronaut.build.internal.jackson-xml-examples"
}

dependencies {
    annotationProcessor(mn.micronaut.inject.java)
    implementation(platform(mn.micronaut.bom))
    implementation(projects.jacksonXml)

    testAnnotationProcessor(mn.micronaut.inject.java)
    testImplementation(mn.micronaut.test.junit5)
    testRuntimeOnly(libs.junit.jupiter.engine)
    testImplementation(mn.micronaut.http.client)
    testRuntimeOnly(mn.micronaut.http.server.netty)
    testRuntimeOnly(libs.logback.classic)
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.named("check") { task ->
    def graal = ["jvmci.Compiler", "java.vendor.version", "java.vendor"].any {
        println "$it ${System.getProperty(it)}"
        System.getProperty(it)?.toLowerCase(Locale.ENGLISH)?.contains("graal")
    }
    if (graal) {
        task.dependsOn("nativeTest")
    }
}

def openGraalModules = [
        "org.graalvm.nativeimage.builder/com.oracle.svm.core.jdk",
        "org.graalvm.nativeimage.builder/com.oracle.svm.core.configure",
        "org.graalvm.sdk/org.graalvm.nativeimage.impl"
]

graalvmNative {
    toolchainDetection = false
    binaries {
        all {
            openGraalModules.each { module ->
                jvmArgs.add("--add-exports=" + module + "=ALL-UNNAMED")
            }
            resources.autodetect()
        }
    }
}
